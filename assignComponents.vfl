string buttons[] = {'P', 'N', 'Cd', 'uv', 'uv1', 'uv2', 'uv3', 'uv4', 'uv5', 'uv6', 'uv7'};
int channels = chi('../channels');
string mode = {'point', 'vertex', 'texture'}[chi('../mode')];


void setAttribComponents(string name; string mode; int elemnum){
    // Set components of attribute from menus
    int size = 3;
    if (name[:-1] == 'uv'){
        size = 2;
    }else if ( name == 'Cd'){
        size = 4;
    }
    
    string pathPattern = '../%s_%i';
    float componentValues[];
    
    // Loop through component parameters
    for(int i=0; i<size; i++){
        string componentName = chs(sprintf(pathPattern, name, i));
        string componentSplit[] = split(componentName, '.');

        // Handle single floats
        if (componentSplit[1] == '')
            componentSplit[1] = 'x';

        float value = 0;

        // Set default to 1 for Alpha
        if (name == 'Cd' && i == 3 && componentName == ' '){
            value = 1;
        }else{
            float attribValue[] = attrib(0, mode, componentSplit[0], elemnum);
            value = attribValue[find('xyzw', componentSplit[1])];
        }
        append(componentValues, value);
    }
    
    //Set Value
    if(name == 'Cd'){
        setattrib(0, mode, name, elemnum, 1, vector(componentValues));
        setattrib(0, mode, 'Alpha', elemnum, 1, componentValues[-1]);  
    }else{
        setattrib(0, mode, name, elemnum, 1, vector(componentValues));
        
        if(name == 'N')
            setattribtypeinfo(0, mode, name, 'normal');
       
        if (name[:-1] == 'uv')
            setattribtypeinfo(0, mode, name, 'texturecoord');
    }
}

// Run through attribute buttons
for(int i=0; i<11; i++){
    // Skip unset buttons
    if (shr(channels, i) % 2 == 0 )
        continue;
        
    string name = buttons[i];
    setAttribComponents(name, mode, @elemnum);
}